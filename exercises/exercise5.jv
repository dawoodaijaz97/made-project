pipeline EnergieStopsPipeline {
    EnergieStopsHttpExtractor
        ->EnergieStopsArchiveInterpreter
        ->EnergieStopsFilePicker
        ->EnergieStopsTextFileInterpreter
        ->EnergieStopsCSVInterpreter
        ->EnergieStopsTableInterpreter
        ->EnergieStopsSQLiteLoader;

    block EnergieStopsHttpExtractor oftype HttpExtractor {
        url: "https://gtfs.rhoenenergie-bus.de/GTFS.zip";
    }

    block EnergieStopsArchiveInterpreter oftype ArchiveInterpreter {
        archiveType: "zip";
    }

    block EnergieStopsFilePicker oftype FilePicker {
        path: "./stops.txt";
    }

    block EnergieStopsTextFileInterpreter oftype TextFileInterpreter {
        encoding: "utf8";
    }

    block EnergieStopsCSVInterpreter oftype CSVInterpreter {
        enclosing: '"';
    }

    constraint CoordinatesRangeConstraint oftype RangeConstraint {
        lowerBound: -90;
        upperBound: 90;
    }

    valuetype coordinates oftype decimal {
        constraints: [
            CoordinatesRangeConstraint,
        ];
    }

    constraint ZoneIdRegex on integer: value == 1925;

    valuetype zoneId oftype integer {
        constraints: [
            ZoneIdRegex,
        ];
    }

    block EnergieStopsTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "stop_id" oftype integer,
            "stop_name" oftype text,
            "stop_lat" oftype coordinates,
            "stop_lon" oftype coordinates,
            "zone_id" oftype zoneId,
        ];
    }

    block EnergieStopsSQLiteLoader oftype SQLiteLoader {
        table: "stops";
        file: "./gtfs.sqlite";
    }
}
